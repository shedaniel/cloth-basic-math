import proguard.gradle.ProGuardTask

buildscript {
    repositories {
        mavenLocal()
        google()
    }

    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.0.1'
    }
}

plugins {
    id 'java'
    id 'maven-publish'
}

group 'me.shedaniel.cloth'
version '0.5.2'

sourceCompatibility = targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.apache.commons:commons-lang3:3.5"
}

processResources {
    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
    inputs.property "version", project.version
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

def proguardJarOut = file(jar.archiveFile.get().getAsFile().absolutePath.replace(".jar", "-min.jar"))

task proguardJar(type: ProGuardTask, dependsOn: jar) {
    configuration './proguard.conf'
    verbose
    injars jar.archiveFile.get().getAsFile()
    outjars proguardJarOut
    libraryjars files(configurations.compileClasspath)
    printmapping 'out.map'
}

build.dependsOn proguardJar
build.dependsOn sourcesJar

publishing {
    publications {
        MyPublication(MavenPublication) {
            artifact(proguardJarOut) {
                builtBy proguardJar
            }
            artifact(sourcesJar)
            groupId 'me.shedaniel.cloth'
            artifactId 'basic-math'
            version = project.version
        }
    }

    repositories {
        if (System.getenv("MAVEN_PASS") != null) {
            maven {
                url = "https://deploy.shedaniel.me/"
                credentials {
                    username = "shedaniel"
                    password = System.getenv("MAVEN_PASS")
                }
            }
        }
    }
}